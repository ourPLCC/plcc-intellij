{
  parserClass="edu.rit.cs.plcc.jetbrainsPlugin.lang.parser_model.PLCCParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="PLCC"
  psiImplClassSuffix="Impl"
  psiPackage="edu.rit.cs.plcc.jetbrainsPlugin.lang.parser_model"
  psiImplPackage="edu.rit.cs.plcc.jetbrainsPlugin.lang.parser_model.impl"

  elementTypeHolderClass="edu.rit.cs.plcc.jetbrainsPlugin.lang.parser_model.PLCCTypes"
  elementTypeClass="edu.rit.cs.plcc.jetbrainsPlugin.lang.parser_model.PLCCElementType"
  tokenTypeClass="edu.rit.cs.plcc.jetbrainsPlugin.lang.parser_model.PLCCTokenType"

  psiImplUtilClass="edu.rit.cs.plcc.jetbrainsPlugin.lang.parser_model.impl.PLCCPsiImplUtil"
}

// For some reason, the parser model returning one space compared to multiple spaces does not work? this is why
// I am currently using SPACES and EOLS. Mabye this is fixed now that I removed TokenType.WHITE_SPACE

// Make sure you remember that this does not operate on longest match first, it is whichever rule is first.

simpleFile ::= regexDefs (SECTION_SEPERATOR EOLS grammarRules)? (SECTION_SEPERATOR EOLS javaInclude)?

fillerLines ::= (COMMENT | EOLS)*

// token type section
regexDefs ::= fillerLines (regexDef EOLS)+
regexDef ::= SPACES? (tokenDef | skipDef) SPACES? COMMENT?
tokenDef ::= (TOKEN? SPACES+)? TOKEN_TYPE_NAME SPACES+ REGEX
skipDef ::= SKIP SPACES+ TOKEN_TYPE_NAME SPACES+ REGEX
//

// grammar rules section
grammarRules ::= fillerLines (grammarRule EOLS)+
grammarRule ::= SPACES? (singleMatch | multiMatch) SPACES?
singleMatch ::= lhs SPACES+ SINGLE_MATCH_RULE (rhs)?
multiMatch ::= lhs SPACES+ ANY_MATCH_RULE (rhs SPACES+ anyMatchSeperator?)?
lhs ::= abstractGrammarDef | concreteGrammarDef
rhs ::= (SPACES+ (namedGrammarUsage | unnamedGrammarUsage | namedCapturedTokenType | unnamedCapturedTokenType| uncapturedTokenType))+
anyMatchSeperator ::= PLUS TOKEN_TYPE_NAME

concreteGrammarDef ::= LEFT_ANGLE_BRACKET GRAMMAR_DEF_NAME RIGHT_ANGLE_BRACKET
abstractGrammarDef ::= concreteGrammarDef COLON INSTANTIATED_NAME

unnamedGrammarUsage ::= LEFT_ANGLE_BRACKET GRAMMAR_USAGE_NAME RIGHT_ANGLE_BRACKET
namedGrammarUsage ::= unnamedGrammarUsage GRAMMAR_USAGE_NAME
unnamedCapturedTokenType ::= LEFT_ANGLE_BRACKET TOKEN_TYPE_NAME RIGHT_ANGLE_BRACKET
namedCapturedTokenType ::= unnamedCapturedTokenType GRAMMAR_USAGE_NAME
uncapturedTokenType ::= TOKEN_TYPE_NAME
//

javaInclude ::= fillerLines (includeStatement EOLS)*
includeStatement ::= SPACES? INCLUDE SPACES+ FILE_NAME SPACES?
